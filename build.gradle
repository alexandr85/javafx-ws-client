buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.edu.sc.seis:macAppBundle:2.1.6'
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:2.3.0'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.macAppBundle'
apply plugin: "edu.sc.seis.launch4j"

group = 'ru.testing'
version = '2.0'
description = """JavaFX application for testing websocket"""
def appFileName = 'WsClient'
def mainClass = 'ru.testing.client.MainApp'
mainClassName = mainClass
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

dependencies {
    compile 'org.glassfish.tyrus:tyrus-client:1.12'
    compile 'org.glassfish.tyrus:tyrus-container-grizzly-client:1.12'
    compile 'com.sun.jersey:jersey-client:1.19'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.h2database:h2:1.3.148'
    compile 'org.slf4j:slf4j-log4j12:1.7.12'
    compile 'com.yuvimasory:orange-extensions:1.3.0'
    compile 'org.controlsfx:controlsfx:8.40.12'
    compile 'org.apache.commons:commons-lang3:3.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task execute(type:JavaExec) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
}

// Create mac bundle with tasks `createApp` or `createDmg`
macAppBundle {
    mainClassName = mainClass
    icon = 'src/main/resources/images/icon-512.icns'
    bundleJRE = false
    appName = appFileName
    dmgName = appFileName
}

// Create windows wrapper with task `createExe`
launch4j {
    mainClassName = mainClass
    icon = '../../application.ico'
    outfile = appFileName + '.exe'
    libraryDir = 'libs'
    outputDir = 'winApp'
}

// Create a single Jar with all dependencies
task fullJar(type: Jar) {
    archiveName = "${appFileName}.jar"
    manifest { attributes "Implementation-Version": version, "Main-Class": mainClass }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    doLast {
        copy {
            from "build/libs/${archiveName}"
            into "build/distributions"
        }
    }
}

// Create application distributions for all os
task create(dependsOn: [clean, createExe, createDmg, fullJar], type: Zip) {
    from 'build/winApp'
    archiveName = "${appFileName}.zip"
    doLast {
        delete "build/libs/${appFileName}.jar"
    }
}
